;@+leo-ver=5-thin
;@+node:ekr.20101027070126.1601: * @thin ../../nsi-postamble.txt
;@@language nsi

Section "Desktop Shortcut" SEC03
  ; Apparently important.
  SetOutPath $INSTDIR
  CreateShortCut "$DESKTOP\${name}.lnk" '"$PythonExecutable"' '"$INSTDIR\launchLeo.py"' "$INSTDIR\leo\Icons\LeoApp.ico" 0
SectionEnd

Section "${ext} File Association"
  SectionIn 1
  SectionIn 2
  SectionIn 3

  # back up old value of extension in case some other program was using it
  ReadRegStr $1 HKCR "${ext}" ""
  StrCmp $1 "" Label1
  StrCmp $1 "${name}File" Label1
  WriteRegStr HKCR "${ext}" "backup_val" $1

Label1:
  WriteRegStr HKCR "${ext}" "" "${name}File"
  WriteRegStr HKCR "${name}File" "" "${name} File"
  WriteRegStr HKCR "${name}File\shell" "" "open"
  WriteRegStr HKCR "${name}File\DefaultIcon" "" '"$INSTDIR\${doc_icon}"'
  WriteRegStr HKCR "${name}File\shell\open\command" "" '"$PythonExecutable" "$INSTDIR\launchLeo.py" "%*"'

SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  CreateDirectory "$SMPROGRAMS\${name}"
  CreateShortCut  "$SMPROGRAMS\${name}\Uninstall.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteRegStr HKLM ${leo_hklm} "" "$INSTDIR"
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${uninst_key}" "DisplayName" "${name}-${version} (remove only)"
  WriteRegStr HKLM "${uninst_key}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${uninst_key}" "DisplayVersion" "${version}"
  WriteRegStr HKLM "${uninst_key}" "URLInfoAbout" "${site}"
  WriteRegStr HKLM "${uninst_key}" "Publisher" "${publisher}"
SectionEnd

;@-leo
